	# zbiór drzwi prowadz¹cych do innych stref po¿arowych
ZoneDoors(d) <=> exists r in Rooms: doorsInRoom(d, r) and  type(r) = "Staircase";
	# zbiór drzwi zapewniaj¹cych  opuszczenie bie¿¹cej strefy
FireSafetyDoors(d) <=>   ExternalDoors(d) or ZoneDoors(d)  ;
   
	# pokoje stanowi¹ce odrêbne strefy po¿arowe
ZoneRooms(r) <=> Rooms(r) and (type(r) = "Staircase");   
	# pomieszczenia które mog¹ wchodzic w sk³ad drogi ewakuacyjnej
EvacRoute(r) <=> Rooms(r) and (type(r) = "Corridor" or type(r) = "Hall");
	# pomieszczenia ktore moga wchodzic w sklad przejscia ewakuacyjnego
AccessPath(r) <=> Rooms(r) and !ZoneRooms(r) and !EvacRoute(r);
  
	# okresla drzwi które s¹ oddalone o co najwy¿ej x od FireSafetyDoors, oddzielone od nich pokojem EvacRoute
validRouteFromAdj(d, x) <=>  Doors(d) and  ( exists d2 in Doors : d2!=d and FireSafetyDoors(d2) and
( exists p in Rooms:  doorsInRoom(d2,p) and doorsInRoom(d,p) and EvacRoute(p) and  x>=doorsDist(d, d2)  ));
  
	# okresla drzwi które s¹ oddalone o co najwy¿ej x od FireSafetyDoors, oddzielone od nich co njawy¿ej n pokojami EvacRoute
validRouteFromInduction(d, x, n)<=>   
 (n = 1 and validRouteFromAdj(d, x) ) or
 (n <> 1 and Doors(d) and validRouteFromInduction(d, x, n-1)) or
 (n <> 1 and Doors(d) and exists d2 in Doors :  d2!=d and
 	(  exists p in Rooms:  
	   		doorsInRoom(d2,p) and 
	   		doorsInRoom(d,p) and 
	   		EvacRoute(p) and 
	   		 x>=doorsDist(d, d2)
	)  and
 	validRouteFromInduction(d2, x - doorsDist(d, d2), n-1)
 ) ;

	# okresla drzwi które s¹ oddalone o co najwy¿ej x od FireSafetyDoors, oddzielone od nich co dowoln¹ liczb¹ pokoi EvacRoute
validRouteFrom(d, x) <=> validRouteFromInduction(d, x, 99);
	# drzwi wchodz¹ce w sk³ad prawid³owej drogi ewakuacyjnej
ValidEvacRouteDoors(d) <=>FireSafetyDoors(d) or  validRouteFrom(d, 30);


	# zbiór pokoi u¿ytkowych posiadaj¹cych drzwi na drogê ewakuacyjn¹
steps0(r) <=>     AccessPath(r)  and ( exists d in ValidEvacRouteDoors: doorsInRoom(d, r) );
	# zbiór pokoi u¿ytkowych s¹siaduj¹cych ze steps0
steps1(r) <=> steps0(r) or ( AccessPath(r)  and (exists r2 in steps0: accessible(r, r2) ));
	# zbiór pokoi u¿ytkowych s¹siaduj¹cych ze steps1
steps2(r) <=> steps1(r) or ( AccessPath(r)  and (exists r2 in steps1: accessible(r, r2) ));
 
	# pokoje wchodz¹ce w sk³ad prawid³owej drogi ewakuacyjnej
ValidEvacRouteRooms(r) <=> EvacRoute(r) and ( exists d in ValidEvacRouteDoors: doorsInRoom(d, r) );

	# suma zbiorów pokoi: 
	# stanowi¹cych odrêbne strefy po¿arowe
	# s¹siaduj¹cych z droga ekwakuacyjn¹ przez mniej ni¿ trzy pomieszczenia
	# wchodz¹cych w sk³ad prawid³owej drogi ewakuacyjnej
ValidRooms(r) <=> ZoneRooms(r) or ValidEvacRouteRooms(r) or steps2(r);

success_msg "wszystkie pokoje ok"
failure_msg "pokoje nie spe³niaja ograniczeñ przeciwpo¿arowych"
forall r in Rooms: ValidRooms(r)
 